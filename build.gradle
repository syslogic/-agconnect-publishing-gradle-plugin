// :buildSrc
buildscript {
    ext {
        github_handle = 'syslogic'
        plugin_display_name = 'AppGallery Connect Publishing Plugin'
        plugin_description = 'It uploads Android APK/ABB packages to the AppGallery Connect Publishing API.'
        plugin_identifier = 'agconnect-publishing-gradle-plugin'
        plugin_class = 'io.syslogic.agconnect.PublishingPlugin'
        plugin_id = 'io.syslogic.agconnect.publishing'
        plugin_version = '1.3.0'
    }
}

plugins {
    id 'org.gradle.java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.0'
}

dependencies {
    implementation "com.android.tools.build:gradle-api:8.1.2"
    implementation 'org.apache.httpcomponents:httpcore:4.4.16'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpmime:4.5.6'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    compileOnly gradleApi()

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.jetbrains:annotations:23.0.0'
    testImplementation gradleTestKit()
    testImplementation(project)
}

gradlePlugin {
    plugins {
        PublishingPlugin {
            id = "${plugin_id}"
            displayName = "${plugin_display_name}"
            description = "${plugin_description}"
            implementationClass = "${plugin_class}"
        }
    }
}

tasks.withType(Jar).configureEach {
    archiveBaseName.set(plugin_identifier)
    archiveVersion.set("${plugin_version}")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

//noinspection ConfigurationAvoidance
task javadocs(type: Javadoc) {
    title = "${plugin_display_name} ${plugin_version} API"
    source = java.sourceSets.main.java.srcDirs
    destinationDir = file("${project.buildDir}/outputs/javadoc/")
    classpath = files(new File(System.getProperty('java.home') + File.separator + 'lib' + File.separator + 'rt.jar'))
    classpath += configurations.implementation
    options.links "https://docs.oracle.com/en/java/javase/17/docs/api/"
    options.linkSource true
    options.author true
    failOnError false
}

//noinspection ConfigurationAvoidance
task javadocJar(type: Jar, dependsOn: javadoc) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

//noinspection ConfigurationAvoidance
task sourcesJar(type: Jar) {
    from java.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

group = 'io.syslogic'
version = "${plugin_version}"
artifacts {
    archives javadocJar
    archives sourcesJar
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = System.getenv("JB_SPACE_MAVEN_REPO")
                credentials {
                    username = System.getenv("JB_SPACE_CLIENT_ID")
                    password = System.getenv("JB_SPACE_CLIENT_SECRET")
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = "${plugin_identifier}"
                from components.getByName("java")
                version = "${plugin_version}"
                pom {
                    name = "${plugin_display_name}"
                    description = "${plugin_description}"
                    url = "https://github.com/${github_handle}/${plugin_identifier}"
                    scm {
                        connection = "scm:git:git://github.com/${github_handle}/${plugin_identifier}.git"
                        developerConnection = "scm:git:ssh://github.com/${github_handle}/${plugin_identifier}.git"
                        url = "https://github.com/${github_handle}/${plugin_identifier}/"
                    }
                }
            }
        }
    }
}
