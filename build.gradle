// :buildSrc
buildscript {
    ext {
        github_handle = 'syslogic'
        plugin_identifier = 'agconnect-publishing-gradle-plugin'
        plugin_version = '10'
        agp_version = '7.2.2'
        hms_version = '1.7.0.300'
        publish_version = '1.0.0'
        annotations_version = '22.0.0'
        httpclient_version = '4.5.13'
        httpcore_version = '4.4.14'
        httpmime_version = '4.5.6'
        gson_version = '2.9.0'
        junit_version = '5.8.2'
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "com.huawei.agconnect:agcp:$hms_version"
        classpath "com.gradle.publish:plugin-publish-plugin:$publish_version"
    }
    repositories {
        google()
        maven { url 'https://developer.huawei.com/repo/' }
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id 'org.gradle.java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version "$publish_version"
}

wrapper {
    distributionType Wrapper.DistributionType.BIN
    version '7.4.2'
}

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

dependencies {

    compileOnly gradleApi()
    implementation "com.android.tools.build:gradle-api:$agp_version"
    implementation "org.jetbrains:annotations:$annotations_version"
    implementation "org.apache.httpcomponents:httpcore:$httpcore_version"
    implementation "org.apache.httpcomponents:httpclient:$httpclient_version"
    implementation "org.apache.httpcomponents:httpmime:$httpmime_version"
    implementation "com.google.code.gson:gson:$gson_version"

    testImplementation gradleTestKit()
    testImplementation "org.jetbrains:annotations:$annotations_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    testImplementation(project)
}

pluginBundle {
    website = "https://github.com/${github_handle}/${plugin_identifier}"
    vcsUrl = "https://github.com/${github_handle}/${plugin_identifier}.git"
    tags = [ 'android', 'huawei', 'appgallery', 'connect', 'publish' ]
}

gradlePlugin {
    plugins {
        PublishingPlugin {
            id = 'io.syslogic.agconnect.publishing'
            displayName = "AppGallery Connect Publishing Plugin"
            description = 'It uploads to Huawei AppGallery Connect Publishing API'
            implementationClass = 'io.syslogic.agconnect.PublishingPlugin'
        }
    }
}

tasks.withType(Jar).configureEach {
    archiveBaseName.set(plugin_identifier)
    archiveVersion.set("${agp_version}.${plugin_version}")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

task javadocs(type: Javadoc) {
    title = "AppGallery Connect Publishing Plugin ${agp_version} API"
    source = java.sourceSets.main.java.srcDirs
    destinationDir = file("${project.buildDir}/outputs/javadoc/")
    configurations.implementation.setCanBeResolved(true)
    classpath = files(new File(System.getProperty('java.home') + File.separator + 'lib' + File.separator + 'rt.jar'))
    classpath += configurations.implementation
    options.links "https://docs.oracle.com/en/java/javase/11/docs/api/"
    options.linkSource true
    options.author true
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from java.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'io.syslogic'
version = "${agp_version}.${plugin_version}"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = "${plugin_identifier}"
                from components.getByName("java")
                version = "${agp_version}.${plugin_version}"
                pom {
                    name = 'AppGallery Connect Publishing Plugin'
                    description = 'It uploads to Huawei AppGallery Connect Publishing API'
                    url = "https://github.com/${github_handle}/${plugin_identifier}"
                    scm {
                        connection = "scm:git:git://github.com/${github_handle}/${plugin_identifier}.git"
                        developerConnection = "scm:git:ssh://github.com/${github_handle}/${plugin_identifier}.git"
                        url = "https://github.com/${github_handle}/${plugin_identifier}/"
                    }
                }
            }
        }
    }
}
